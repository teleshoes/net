#!/usr/bin/perl
use strict;
use warnings;

my $STATUS_WLAN = "wlan";
my $STATUS_ETH = "eth";
my $STATUS_PPP = "ppp";
my $STATUS_WCONNECT = "wconnect";
my $STATUS_WAUTO = "wauto";
my $STATUS_TETHERING = "tethering";
my $STATUS_NONE = "none";

my $statusDevs = {
  $STATUS_WLAN => [qw(wl wlan)],
  $STATUS_ETH  => [qw(en eth)],
  $STATUS_PPP  => [qw(ppp)],
};

my @allStatuses = ($STATUS_WLAN, $STATUS_ETH, $STATUS_PPP,
  $STATUS_WCONNECT, $STATUS_WAUTO, $STATUS_TETHERING, $STATUS_NONE);

my $usage = "Usage:
  $0
    print one of:  [" . join(",", @allStatuses) . "]
";

sub getDev();
sub isWconnect();
sub isWauto();
sub isTethering();

sub main(@){
  die $usage if @_ > 0;
  my $devStatus = getDev();

  my $status = undef;
  $status = $devStatus if not defined $status and defined $devStatus;
  $status = $STATUS_WCONNECT if not defined $status and isWconnect();
  $status = $STATUS_WAUTO if not defined $status and isWauto();
  $status = $STATUS_TETHERING if not defined $status and isTethering();
  $status = $STATUS_NONE if not defined $status;
  print "$status\n";
}

sub getDev(){
  my $netstat = `ifconfig -s`;

  for my $status(sort keys %$statusDevs){
    my @devs = @{$$statusDevs{$status}};
    my $device = `ifdev @devs 2>/dev/null`;
    chomp $device;
    if($? == 0 and $netstat =~ /^$device /m){
      if($status eq 'wlan'){
        my $iwconfig = `iwconfig $device`;
        if($iwconfig =~ /ESSID:off\/any\s*$/m){
          next;
        }
      }
      if(`ifconfig $device` =~ /inet( addr)? \d+/){
        return $status;
      }
    }
  }
  return undef;
}

sub isWconnect(){
  system "pgrep wconnect > /dev/null 2>/dev/null";
  return $? == 0;
}

sub isWauto(){
  system "pgrep wauto > /dev/null 2>/dev/null";
  return $? == 0;
}

sub isTethering(){
  system "pidof pppd wvdial > /dev/null 2>/dev/null";
  return $? == 0;
}

&main(@ARGV);
