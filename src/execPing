#!/usr/bin/perl
use strict;
use warnings;

sub attemptPing($$$$);
sub logWatchdog($);

my $defaultDeadline = 1;
my $defaultDelay = 1;
my $defaultTimeout = 0;
my $defaultAttempts = 0;
my $defaultWatchdogAttempts = 5;

my $watchdogLog = "/tmp/ping_watchdog.log";

my $defaultUrl = "fsf.org";

my $usage = "Usage:
  $0 [OPTS]
    Tests if $defaultUrl is ping-able. Prints \"ok: URL\" or \"failed: URL\"

  $0 [OPTS] EXEC [ARG ARG ..]
    Ensure that $defaultUrl is ping-able before running \"EXEC ARG ARG ..\"

  OPTS:
    --url=URL
      use URL instead of $defaultUrl

    --watchdog
      Instead of normal behaviour, ping URL forever.
      If URL fails to ping after WATCHDOG_ATTEMPTS attempts,
        run the given command, wait for it to finish,
        and resume pinging forever.
      Will not run (or rerun) the failure command until a successful ping.
      Logs failures to $watchdogLog.
      Must specify a command.
      Ignores --attempts and --timeout arguments.

    --watchdog-attempts=INTEGER {default is $defaultWatchdogAttempts}
      sets the number of attempts before running the given command

    --deadline=INTEGER          {default is $defaultDeadline}
      seconds to wait for a response for each ping

    --delay=INTEGER             {default is $defaultDelay}
      seconds to wait between failed pings

    --timeout=INTEGER           {default is $defaultTimeout}
      seconds to wait before failing, 0 means forever

    --attempts=INTEGER          {default is $defaultAttempts}
      number of times to attempt a ping, 0 means forever

    --ipmagic=IPMAGIC_CONF
      use IPMAGIC_CONF as an ipmagic config, and use
        `ipmagic IPMAGIC_CONF --ip` as URL
      also, if the IP is not ping-able, tries SSH as a fallback

    -q|--quiet
      print less to STDOUT and STDERR
";

sub main(@){
  my ($url, $deadline, $delay, $timeout, $attempts, $watchdogAttempts) =
    ($defaultUrl, $defaultDeadline, $defaultDelay, $defaultTimeout,
     $defaultAttempts, $defaultWatchdogAttempts);
  my $watchdog = 0;
  my $ipmagicConf = undef;
  my $quiet = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $opt = shift;
    if($opt =~ /^--url=(.*)$/){
      $url = $1;
    }elsif($opt =~ /^--deadline=(\d+)$/){
      $deadline = $1;
    }elsif($opt =~ /^--delay=(\d+)$/){
      $delay = $1;
    }elsif($opt =~ /^--timeout=(\d+)$/){
      $timeout = $1;
    }elsif($opt =~ /^--attempts=(\d+)$/){
      $attempts = $1;
    }elsif($opt =~ /^--ipmagic=(.*)$/){
      $ipmagicConf = $1;
    }elsif($opt =~ /^--watchdog-attempts=(\d+)$/){
      $watchdogAttempts = $1;
    }elsif($opt =~ /^--watchdog$/){
      $watchdog = 1;
    }elsif($opt =~ /^(-q|--quiet)$/){
      $quiet = 1;
    }else{
      die $usage;
    }
  }

  my $exec = shift;
  my @args = @_;

  my $testOnly = not defined $exec;

  if($watchdog){
    die $usage if $testOnly;
    my $failed = 1;
    while(1){
      my $rem = $watchdogAttempts;
      while(not attemptPing $url, $ipmagicConf, $deadline, $quiet){
        if($rem == 0){
          last;
        }
        $rem--;
        print STDERR `date` if not $quiet;
        print STDERR "ping failed, retrying...\n" if not $quiet;
        sleep $delay;
      }
      my $prevFailed = $failed;
      $failed = $rem == 0;

      if(not $prevFailed and $failed){
        print STDERR `date` if not $quiet;
        print STDERR "ping failure, running $exec @args\n" if not $quiet;
        logWatchdog "ping failure, running $exec @args";
        my @cmd = ($exec, @args);
        s/'/'\\''/g foreach @cmd;
        @cmd = map {"'$_'"} @cmd;
        system "@cmd | tee -a $watchdogLog";
      }elsif($prevFailed and $failed){
        print STDERR `date` if not $quiet;
        print STDERR "repeated ping failure, NOT running command\n" if not $quiet;
        logWatchdog "repeated ping failure, NOT running command";
      }elsif($prevFailed and not $failed){
        print STDERR `date` if not $quiet;
        print STDERR "ping recovered...\n" if not $quiet;
        logWatchdog "ping recovered";
      }
    }
  }

  my $start = time;
  my $count = 0;
  while(not attemptPing $url, $ipmagicConf, $deadline, $quiet){
    $count++;
    my $isTimeout = $timeout > 0 && time - $start > $timeout;
    my $isMaxAttempts = $attempts > 0 && $count >= $attempts;
    if($isTimeout or $isMaxAttempts){
      if($testOnly){
        print "failed: $url\n";
        exit 1;
      }else{
        die "timeout reached, exiting\n";
      }
    }
    print STDERR "ping failed, retrying...\n" if not $quiet;
    sleep $delay;
  }

  if($testOnly){
    print "ok: $url\n";
    exit 0;
  }else{
    exec $exec, @args;
  }
}

sub logWatchdog($){
  my $msg = shift;
  my $d = `date`;
  chomp $d;
  open FH, ">> $watchdogLog";
  print FH "$d $msg\n";
  close FH;
}

sub attemptPing($$$$){
  my ($url, $ipmagicConf, $deadline, $quiet) = @_;
  if(defined $ipmagicConf){
    $url = `ipmagic "$ipmagicConf" --ip`;
    chomp $url;
    print "Using \"$url\"\n" if not $quiet;
  }
  $url =~ s/'/'\\''/g;
  system "ping -c 1 -w $deadline -W $deadline '$url' >/dev/null 2>/dev/null";
  my $success = $? == 0;
  if(not $success and defined $ipmagicConf){
    system "ipmagic", $ipmagicConf, "echo ssh $ipmagicConf success!\n";
    $success = $? == 0;
  }
  return $success;
}

&main(@ARGV);
