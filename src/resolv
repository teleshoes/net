#!/usr/bin/perl
#Copyright 2010,2012,2015 Elliot Wolk
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
use strict;
use warnings;

sub getAvailableConfigs();
sub removeResolv();
sub replaceResolv($);

my $home = defined $ENV{HOME} ? $ENV{HOME} : "";

my $targetFile = '/etc/resolv.conf';
my $configDir = "$home/resolv";
my $okConfigs = join "|", getAvailableConfigs();

my $usage = "Usage:
  $0 [CONFIG_NAME]   {available: $okConfigs}
    Copies $configDir/resolv.conf.CONFIG_NAME to $targetFile,
      and makes it read-only with chattr +i.
    CONFIG_NAME must be [a-z]+ {lowercase letters only}.
    'default' is assumed if CONFIG_NAME is omitted.

  $0 -s FILE_NAME
    Copies FILE_NAME to $targetFile
      and makes it read-only with chattr +i

  $0 -d
    Removes $targetFile
";


sub main(@){
  if(not -d $configDir){
    print STDERR "$configDir does not exist, skipping resolv\n";
    exit 0;
  }

  if(@_ == 1 and $_[0] eq "-d"){
    removeResolv();
    exit 0;
  }elsif(@_ == 2 and $_[0] eq "-s"){
    my $file = $_[1];
    die "file not found: $file\n" if not -f $file;
    print "reolv: applying $file:\n====\n";
    system "cat", $file;
    replaceResolv $file;
    exit 0;
  }

  my $conf = shift;
  $conf = 'default' if not defined $conf;

  if (@_ > 0 or $conf !~ /^($okConfigs)$/){
    die $usage;
  }
  my $file = "$configDir/resolv.conf.$conf";
  print "resolv: applying $conf";
  if(-l $file){
    my $link = `ls -l $file`;
    if($link =~ /-> .*resolv\.conf\.([a-z]+)/){
      my $realChoice = $1;
      print " => $realChoice";
    }
  }
  print "\n";

  replaceResolv $file;
}

sub getAvailableConfigs(){
  my @configs;
  for my $file(`ls -1 $configDir/ 2>/dev/null`){
    push @configs, $1 if $file =~ /resolv\.conf\.([a-z]+)/;
  }
  return @configs;
}

sub removeResolv(){
  if(-e $targetFile){
    system "chattr", "-i", $targetFile;
    system "rm", "-f", $targetFile;
  }
}

sub replaceResolv($){
  my $file = shift;
  removeResolv();
  system "cp", $file, $targetFile;
  system "chattr", "+i", $targetFile;
}

&main(@ARGV);
