#!/usr/bin/perl
use strict;
use warnings;

sub run(@);
sub checkCarrier($);
sub up($);
sub down($);

my $carrierAttempts = 10;

sub main(@){
  if(`whoami` ne "root\n"){
    print STDERR "rerunning as root...\n";
    exec "sudo", "$0", @ARGV;
  }

  my $arg = shift;
  $arg = 'on' if not defined $arg;

  die "$0 [on|off] {default: on}" if @_ > 0 or $arg !~ /^(on|off)$/;

  my $dev = `ifdev eth`;
  chomp $dev;
  if($dev !~ /^eth/){
    die "no suitable device {i.e. named eth#} found\n";
  }

  if($arg eq 'off'){
    down($dev);
  }elsif($arg eq 'on'){
    up($dev);
    if(not checkCarrier($dev)){
      down($dev);
      up($dev);
      print "waiting for carrier, giving up after $carrierAttempts tries\n";
      select(STDOUT);
      $| = 1;
      while($carrierAttempts > 0 and not checkCarrier($dev)){
        print ".";
        sleep 1;
        $carrierAttempts--;
      }
    }
    die "\nerror: cable is disconnected\n" if not checkCarrier $dev;
    run "dhclient $dev";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

sub checkCarrier($){
  my $dev = shift;
  my $carrier = `cat "/sys/class/net/$dev/carrier" 2>/dev/null`;
  chomp $carrier;
  if($carrier eq "1"){
    return 1;
  }else{
    return 0;
  }
}

sub up($){
  my $dev = shift;
  print "\nconnecting wired $dev\n";
  run "ifconfig", $dev, "up";
}

sub down($){
  my $dev = shift;
  print "\ndisconnecting wired $dev\n";
  run "pkill", "-f", "^dhclient $dev\$";
  run "ifconfig", $dev, "down";
}

&main(@ARGV);
