#!/usr/bin/perl
use strict;
use warnings;

sub readIfconfigDevNames();
sub readWinfoDevMappings();

my $usage = "Usage:
  $0 DEV_TYPE [DEV_TYPE DEV_TYPE ...]
     finds a net device to match any DEV_TYPE,
       using `winfo --att=dev` and `ifconfig -a`

     if winfo has a dev mapping for any <DEV_TYPE>
       then print the first mapped device
     otherwise, if any ifconfig device name matches:
       \"<DEV_TYPE>#\" or \"<DEV_TYPE>p#s#\" for any <DEV_TYPE>
       then print the first one that matches

     DEV_TYPE can contain only letters, numbers, and underscores

     e.g.:
       if `winfo --att=dev` returns \"en:eth0,wl:wlan6\":
         then `$0 wlan wl eth` => print \"wlan6\"

       if `winfo --att=dev` returns \"\",
       and `ifconfig -a` has a line like \"wlan0: flags...\" in it
         then `$0 wl wlan` => print \"wlan0\"

       if `winfo --att=dev` returns \"\",
       and `ifconfig -a` has a line like \"wlp0s0: flags...\" in it
         then `$0 wlan wl` => print \"wlp0s0\"
";

sub main(@){
  my @devTypes = @_;
  die $usage if @devTypes == 0;
  for my $devType(@devTypes){
    die $usage if $devType !~ /^\w+$/;
  }

  my $winfoDevMappings = readWinfoDevMappings();
  for my $devType(@devTypes){
    if(defined $$winfoDevMappings{$devType}){
      my $devName = $$winfoDevMappings{$devType};
      print "$devName\n";
      exit 0;
    }
  }

  my @ifconfigDevs = readIfconfigDevNames();
  for my $devType(@devTypes){
    for my $ifconfigDevName(@ifconfigDevs){
      if($ifconfigDevName =~ /^(${devType}\d+|${devType}p\d+s\d+)$/){
        print "$ifconfigDevName\n";
        exit 0;
      }
    }
  }

  my $devNameFmt = join " or ", map {"'${_}#' or '${_}p#s#'"} @devTypes;
  die "No device named $devNameFmt found\n";
}

sub readIfconfigDevNames(){
  my @ifconfigDevNames;
  for my $line(`ifconfig -a`){
    if($line =~ /^(\w+):/){
      my $ifconfigDevName = $1;
      push @ifconfigDevNames, $ifconfigDevName;
    }
  }
  return @ifconfigDevNames;
}

sub readWinfoDevMappings(){
  my $winfoDev = `winfo --att=dev`;
  chomp $winfoDev;
  $winfoDev =~ s/\s+//g;
  my @devPairs = split /,/, $winfoDev;
  my $winfoDevs = {};
  for my $pair(@devPairs){
    if($pair !~ /^(\w+):(\w+)$/){
      die "Malformed device pair in dev att {'prefix:device'}: '$pair'\n";
    }
    my ($devType, $devName) = ($1, $2);
    $$winfoDevs{$devType} = $devName;
  }
  return $winfoDevs;
}

&main(@ARGV);
