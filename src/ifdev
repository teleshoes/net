#!/usr/bin/perl
use strict;
use warnings;

my $usage = "Usage:
  $0 DEV_NAME
     finds a net device to match DEV_NAME,
       using `winfo --att=dev` and `ifconfig -a`

     if winfo has a dev mapping for <DEV_NAME>
       then print the mapped device
     otherwise, if any ifconfig device name matches:
       \"<DEV_NAME>#\"
       then print the first one that matches

     DEV_NAME can contain only letters, numbers, and underscores

     e.g.:
       if `winfo --att=dev` returns \"en:eth0,wl:wlan6\":
         then `$0 wlan` => print \"wlan6\"

       if `winfo --att=dev` returns \"\",
       and `ifconfig -a` has a line like \"wlan0: flags...\" in it
         then `$0 wlan` => print \"wlan0\"
";

sub main(@){
  my $dev = shift;
  die $usage if not defined $dev;

  my $winfoDev = `winfo --att=dev`;
  chomp $winfoDev;
  $winfoDev =~ s/\s+//g;
  my @devPairs = split /,/, $winfoDev;
  for my $pair(@devPairs){
    if($pair !~ /^(\w+):(\w+)$/){
      die "Malformed device pair in dev att {'prefix:device'}: '$pair'\n";
    }
    my($prefix, $device) = ($1, $2);
    if($prefix eq $dev){
      print "$device\n";
      exit 0;
    }
  }

  my @ifconfig = `ifconfig -a`;

  for my $line(@ifconfig){
    if($line =~ /^($dev\d+):? /){
      print "$1\n";
      exit 0;
    }
  }

  die "No device named $dev found\n";
}

&main(@ARGV);
