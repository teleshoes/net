#!/usr/bin/perl
use strict;
use warnings;

sub selectFile(@);
sub dieConfig($;$);
sub unquote($);
sub parseLine($);
sub fuzzy($);

my $home = defined $ENV{HOME} ? $ENV{HOME} : '';

my $netConf = selectFile ("$home/wifi.conf", "/etc/wifi.conf");
my $autoConf = selectFile ("$home/wifi-auto.conf", "/etc/wifi-auto.conf");

my $quotedStr = '"(?:[^"]|"")*"';
my $unquotedStr = '[^ \t"]*';
my $str = "(?:$quotedStr|$unquotedStr)";
my $lineRegex = qr/^\s*($str)\s*($str)\s*($str)\s*($str)\s*\n$/;

my $wscanExec = "wscan";
my $validEncs = "wep|wpa|none";
my $encDefault = 'none';
my $validModes = "ad-hoc|managed|master|repeater|secondary|monitor|auto";
my $modeDefault = 'managed';
my $okAtts = join "|", qw( dev driver );

my $usage = "Usage:
  $0 [OPTS] SSID
    match the argument against $netConf,
      without regard to case, and with '_' matching ' ' or '_'.
    prints exact ssid, enc, key, mode, and auto
  $0 [OPTS] --scanned SSID
    match the argument against $netConf,
      without regard to case, and with '_' matching ' ' or '_'.
    prints exact ssid, enc, key, mode, and auto
  $0 [OPTS] --list-auto
    prints all auto-connectable SSIDs in config and their priority
    SSIDs are printed in lowercase, with ' ' replaced with '_'
  $0 [OPTS] --list|--list-all
    prints all SSIDs in config
    SSIDs are printed in lowercase, with ' ' replaced with '_'
  $0 [OPTS] --att=ATT_NAME
    find 'ATT_NAME=' line in $netConf
    print the value if it is present, or nothing if it is absent
    valid ATT_NAMEs: $okAtts
      driver: the driver for wpa_supplicant {see wpa_supplicant -h}
      dev:    a comma-separated list of \"DEV_TYPE:DEV_NAME\" pairs
              these are read by ifdev and override using ifconfig
              e.g.:   dev=wl:wlp2s3,wlan:eth4,eth:eth5

  OPTS
    --exact
      Always print exact ssid, and match ssid exactly
";

my $format = ''
  . "Lines are either attributes or wireless entries\n"
  . "Valid attributes: $okAtts\n"
  . "Wireless Entries are one per line:\n"
  . "SSID ENC KEY MODE\n"
  . "Attributes are whitespace-separated, doublequoted strings\n"
  . " any amount of leading and trailing whitespace/tabs is permitted\n"
  . " a literal doublequote character is represented by \"\"\n"
  . " double-quotes may be omitted if an att has no \"s or whitespace\n"
  . "Lines beginning with #s {or some whitespace and then a #} are ignored\n"
  . "SSID: <required> essid\n"
  . "ENC:  <optional> encryption mode {default = \"$encDefault\"}\n"
  . "      one of $validEncs \n"
  . "KEY:  <optional> wpa or wep key {default = \"\"}\n"
  . "      ASCII wep keys need 's:' at the front. hex keys do not.\n"
  . "      wpa PSK/TKIP keys cannot be given in hex; this is bad, probably\n"
  . "MODE: <optional> wifi mode {default = \"$modeDefault\"}\n"
  . "      one of $validModes\n"
;

sub main(@){
  my $exact = 0;
  $exact = shift if @_ >1 and $_[0] eq '--exact';
  my ($targetSSID, $scanned, $listAll, $listAuto, $att);
  if(@_ == 1 and $_[0] =~ /^(--list|--list-all)$/){
    $listAll = 1;
  }elsif(@_ == 1 and $_[0] eq '--list-auto'){
    $listAuto = 1;
  }elsif(@_ == 2 and $_[0] eq '--scanned'){
    $scanned = shift;
    $targetSSID = shift;
  }elsif(@_ == 1 and $_[0] =~ /^--att=($okAtts)$/){
    shift;
    $att = $1;
  }elsif(@_ == 1){
    $targetSSID = shift;
  }else{
    die $usage;
  }

  my %entries;
  my %fuzzyEntries;
  my %attributes;
  open FH, "< $netConf" or die "Could not open $netConf for reading\n";
  my @lines = <FH>;
  close FH;
  for my $line(@lines){
    next if $line =~ /^\s*(#|$)/;
    if($line =~ /^\s*($okAtts)\s*=\s*(.*?)\s*$/){
      $attributes{$1} = $2;
      next;
    }
    my $entry = parseLine $line;
    my $ssid = $$entry[0];
    $entries{$ssid} = $entry;
    $fuzzyEntries{fuzzy($ssid)} = $entry;
  }

  if(defined $att){
    print "$attributes{$att}\n" if defined $attributes{$att};
    exit 0;
  }

  my $count = 1;
  my @autoLines = `cat $autoConf 2>/dev/null` if defined $autoConf;
  my %autos = map{chomp; ($_ => $count++)} @autoLines;

  if(defined $listAll){
    my @ssids = sort $exact ? keys %entries : keys %fuzzyEntries;
    print join("\n", @ssids) . "\n";
  }elsif(defined $listAuto){
    for my $ssid(sort {$autos{$a} <=> $autos{$b}} keys %autos){
      my $ssidFmt = $exact ? $ssid : fuzzy($ssid);
      print "$autos{$ssid}:$ssidFmt\n";
    }
  }elsif(defined $scanned and defined $targetSSID){
    for my $cell(`$wscanExec -l --info`){
      if($cell =~ /^(.*),(\d+\%),(ch\d+),($validEncs),($validModes),(.*)$/i){
        my ($ssid, $qual, $chan, $enc, $mode, $note) = ($1, $2, $3, $4, $5, $6);
        my $found = 0;
        $found = 1 if $exact and $ssid eq $targetSSID;
        $found = 1 if not $exact and fuzzy($ssid) eq fuzzy($targetSSID);
        if($found){
          print "ssid:$ssid\nenc:$enc\nmode:$mode\n";
        }
      }
    }
  }elsif(defined $targetSSID){
    my $entry = $entries{$targetSSID};
    if(not defined $entry and not $exact){
      $entry = $fuzzyEntries{fuzzy $targetSSID};
    }
    if(defined $entry){
      my ($ssid, $enc, $key, $mode) = @{$entry};
      my $auto = defined $autos{$ssid} ? $autos{$ssid} : '';
      print "ssid:$ssid\nenc:$enc\nkey:$key\nmode:$mode\nauto:$auto\n";
    }else{
      print STDERR "SSID NOT FOUND: $targetSSID\n";
    }
  }
}

sub dieConfig($;$){
  my $line = shift();
  my $reason = shift() || '';
  die "Malformed line in $netConf\n$format\nLine: '$line'\n$reason\n";
}
sub unquote($){
  my $s = shift;
  $s =~ s/^"|"$//g;
  $s =~ s/""/"/g;
  return $s;
}
sub parseLine($){
  my $line = shift;
  if($line =~ $lineRegex){
    my ($ssid, $enc, $key, $mode) = map {unquote $_} ($1, $2, $3, $4);
    $enc = $encDefault if $enc eq '';
    $mode = $modeDefault if $mode eq '';
    if(length $ssid == 0){
      dieConfig $line, "Error: SSID is empty";
    }elsif($enc !~ /^($validEncs)$/i){
      dieConfig $line, "Error: invalid: ENC $enc\n";
    }elsif($mode !~ /^($validModes)$/i){
      dieConfig $line, "Error: invalid: MODE $mode\n";
    }
    return [$ssid, $enc, $key, $mode];
  }else{
    dieConfig $line;
  }
}
sub fuzzy($){
  my $s = shift;
  $s = lc $s;
  $s =~ tr/ /_/;
  return $s;
}
sub selectFile(@){
  for my $file(@_){
    return $file if -e $file;
  }
  return undef;
}

&main(@ARGV);
